apply plugin: 'com.android.library'
apply plugin: 'com.jfrog.bintray'

android {
    compileSdkVersion Integer.parseInt(project.ANDROID_BUILD_SDK_VERSION)
    buildToolsVersion project.ANDROID_BUILD_TOOLS_VERSION

    defaultConfig {
        minSdkVersion Integer.parseInt(project.ANDROID_BUILD_MIN_SDK_VERSION)
        targetSdkVersion Integer.parseInt(project.ANDROID_BUILD_TARGET_SDK_VERSION)
        consumerProguardFiles 'proguard-rules.pro'
    }

    buildTypes {
        release {}
        debug {}
    }

    lintOptions {
        abortOnError true
        baseline file("../lint/lint-baseline-swrvesdkcommon.xml")
    }

    // This lib cannot use 1.8 as it is used by Unity and it would force
    // the app to create a custom .gradle file
}

dependencies {
    implementation 'androidx.legacy:legacy-support-v4:' + project.ANDROIDX_LEGACY_SUPPORT_V4_VERSION
    implementation 'com.google.code.gson:gson:' + project.GSON_VERSION
    implementation 'com.jakewharton.timber:timber:' + project.TIMBER_VERSION
    implementation 'androidx.work:work-runtime:' + project.ANDROIDX_WORK_RUNTIME_VERSION
}

apply plugin: 'maven-publish'
apply plugin: 'signing'

task androidCommonJavadoc(type: Javadoc, dependsOn: "assembleRelease") {
    description "Generates Javadoc for release."
    ext.androidJar = "${android.sdkDirectory}/platforms/${android.compileSdkVersion}/android.jar"
    classpath += files(ext.androidJar)
    failOnError true
    include 'com/swrve/**', 'androidx/core/app/SwrveJobIntentService.java'

    // Use the release variant classpath
    android.libraryVariants.all { variant ->
        if (variant.name == "release") {
            source = variant.sourceSets.collect { it.java.sourceFiles }.inject { t, fc -> t + fc }
            doFirst {
              classpath += files(variant.javaCompileProvider.get().classpath.files)
            }
        }
    }
}
task androidCommonJavadocsJar(type: Jar, dependsOn: androidCommonJavadoc) {
    description "Package Javadoc for Common."
    classifier = 'javadoc'
    from androidCommonJavadoc
}
task androidCommonSourcesJar(type: Jar) {
    description "Package sources for Common."
    classifier = 'sources'
    from files(android.sourceSets.main.java.srcDirs)
}

if (project.hasProperty('SWRVE_COMMON_ARTIFACT_ID')) {
    publishing {
        repositories {
            maven {
                url "$buildDir/../publish" // SwrveCommonSDK/publish
            }
        }

        publications {
            maven(MavenPublication) {
                artifactId project.SWRVE_COMMON_ARTIFACT_ID
                groupId project.SWRVE_GROUP_ID
                version project.SWRVE_SDK_VERSION
                artifacts = ["$buildDir/outputs/aar/${project.name}-release.aar", androidCommonJavadocsJar, androidCommonSourcesJar]

                // The publication doesn't know about our dependencies due to components.java not being present in aar pieces
                // so we have to manually add them to the pom
                pom.withXml {
                    def dependenciesNode = asNode().appendNode('dependencies')

                    // Iterate over the compile dependencies (we don't want the test ones), adding a <dependency> node for each
                    configurations.getByName("releaseCompileClasspath").allDependencies.each {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                        dependencyNode.appendNode('scope', 'compile')
                    }
                }
            }
        }
    }

    bintray {
        user = System.getenv('BINTRAY_USER')
        key = System.getenv('BINTRAY_KEY')
        pkg {
            repo = 'android'
            name = System.getProperty('bintray.pkg.name') == null ? project.SWRVE_COMMON_ARTIFACT_ID : System.getProperty('bintray.pkg.name')
            userOrg = 'swrve-inc'
            licenses = ['Apache-2.0']
            desc = 'Swrve SDK'
            websiteUrl = "https://www.swrve.com"
            publish = true
            version {
                name = project.SWRVE_SDK_VERSION
            }
        }
        publications = ['maven']

    }
}
