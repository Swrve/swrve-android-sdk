apply plugin: 'com.android.library'
apply plugin: 'com.jfrog.bintray'

android {
    compileSdkVersion Integer.parseInt(project.ANDROID_BUILD_SDK_VERSION)
    buildToolsVersion project.ANDROID_BUILD_TOOLS_VERSION

    defaultConfig {
        minSdkVersion Integer.parseInt(project.ANDROID_BUILD_MIN_SDK_VERSION)
        targetSdkVersion Integer.parseInt(project.ANDROID_BUILD_TARGET_SDK_VERSION)
        consumerProguardFiles 'proguard-rules.pro'
    }

    buildTypes {
        release {}
        debug {}
    }

    flavorDimensions "swrve-sdk"

    productFlavors {
        core {
            flavorDimensions "swrve-sdk"
        }
        amazon {
            flavorDimensions "swrve-sdk"
            consumerProguardFiles 'amazon-proguard-rules.pro'
        }
        firebase {
            flavorDimensions "swrve-sdk"
        }
        huawei {
            flavorDimensions "swrve-sdk"
            consumerProguardFiles 'huawei-proguard-rules.pro'
            minSdkVersion Integer.parseInt(project.HUAWEI_BUILD_MIN_SDK_VERSION)
        }
    }

    sourceSets {
        firebase {
            java.srcDirs += java.srcDirs += 'src/google/java/'
        }
        huawei {
            java.srcDirs += java.srcDirs += 'src/google/java/'
        }
    }

    lintOptions {
        abortOnError true
        baseline file("../lint/lint-baseline-swrvesdk.xml")
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    api project(':SwrveSDKCommon')
    api project(':SwrveConversationSDK')
    api 'androidx.legacy:legacy-support-v4:' + project.ANDROIDX_LEGACY_SUPPORT_V4_VERSION
    api 'com.google.code.gson:gson:' + project.GSON_VERSION
    api 'androidx.work:work-runtime:' + project.ANDROIDX_WORK_RUNTIME_VERSION
    // The amazon-device-messaging jar is not distributed with Swrve source code and a stubbed out
    // version is used instead. If compiling amazon flavour from source you should delete the
    // stubbed version in providedLibs, and replace with official jar downloaded from the Amazon
    // Developer portal (https://developer.amazon.com/device-messaging).
    amazonCompileOnly fileTree(dir: "providedLibs", include: ['amazon-device-messaging*.jar'])
    firebaseApi 'com.google.android.gms:play-services-ads-identifier:' + project.PLAY_SERVICES_ADS_VERSION
    firebaseApi 'com.google.firebase:firebase-messaging:' + project.FIREBASE_MESSAGING_VERSION
    huaweiApi 'com.google.android.gms:play-services-ads-identifier:' + project.PLAY_SERVICES_ADS_VERSION
    huaweiApi 'com.google.firebase:firebase-messaging:' + project.FIREBASE_MESSAGING_VERSION
    huaweiApi 'com.huawei.hms:push:' + project.HMS_PUSH_KIT_VERSION
}

apply plugin: 'maven-publish'
apply plugin: 'signing'

task androidCoreJavadoc(type: Javadoc, dependsOn: "assembleCoreRelease") {
    description "Generates Javadoc for Core release."
    ext.androidJar = "${android.sdkDirectory}/platforms/${android.compileSdkVersion}/android.jar"
    classpath += files(ext.androidJar)
    failOnError true
    include 'com/swrve/**'

    // Use the release variant classpath
    android.libraryVariants.all { variant ->
        if (variant.name == "coreRelease") {
            source = variant.sourceSets.collect { it.java.sourceFiles }.inject { t, fc -> t + fc }
            doFirst {
              classpath += files(variant.javaCompileProvider.get().classpath.files)
            }
        }
    }
}
task androidCoreJavadocsJar(type: Jar) {
    description "Package Javadoc for Core."
    classifier = 'javadoc'
    from androidCoreJavadoc
}
task androidCoreSourcesJar(type: Jar) {
    description "Package sources for Core."
    classifier = 'sources'
    from files(android.sourceSets.main.java.srcDirs) + files(android.sourceSets.core.java.srcDirs)
}

task androidAmazonJavadoc(type: Javadoc, dependsOn: "assembleAmazonRelease") {
    description "Generates Javadoc for Amazon release."
    ext.androidJar = "${android.sdkDirectory}/platforms/${android.compileSdkVersion}/android.jar"
    classpath += files(ext.androidJar)
    failOnError true
    include 'com/swrve/**'

    // Use the release variant classpath
    android.libraryVariants.all { variant ->
        if (variant.name == "amazonRelease") {
            source = variant.javaCompileProvider.get().source
            classpath += files(variant.javaCompileProvider.get().classpath)
        }
    }
}
task androidAmazonJavadocsJar(type: Jar) {
    description "Package Javadoc for Amazon."
    classifier = 'javadoc'
    from androidAmazonJavadoc
}
task androidAmazonSourcesJar(type: Jar) {
    description "Package sources for Amazon."
    classifier = 'sources'
    from files(android.sourceSets.main.java.srcDirs) + files(android.sourceSets.amazon.java.srcDirs)
}

task androidFirebaseJavadoc(type: Javadoc, dependsOn: "assembleFirebaseRelease") {
    description "Generates Javadoc for Firebase release."
    ext.androidJar = "${android.sdkDirectory}/platforms/${android.compileSdkVersion}/android.jar"
    classpath += files(ext.androidJar)
    failOnError true
    include 'com/swrve/**'

    // Use the release variant classpath
    android.libraryVariants.all { variant ->
        if (variant.name == "firebaseRelease") {
            source = variant.javaCompileProvider.get().source
            classpath += files(variant.javaCompileProvider.get().classpath)
        }
    }
}
task androidFirebaseJavadocsJar(type: Jar) {
    description "Package Javadoc for Firebase."
    classifier = 'javadoc'
    from androidFirebaseJavadoc
}
task androidFirebaseSourcesJar(type: Jar) {
    description "Package sources for Firebase."
    classifier = 'sources'
    from files(android.sourceSets.main.java.srcDirs) + files(android.sourceSets.firebase.java.srcDirs)
}

task androidHuaweiJavadoc(type: Javadoc, dependsOn: "assembleHuaweiRelease") {
    description "Generates Javadoc for Huawei release."
    ext.androidJar = "${android.sdkDirectory}/platforms/${android.compileSdkVersion}/android.jar"
    classpath += files(ext.androidJar)
    failOnError true
    include 'com/swrve/**'

    // Use the release variant classpath
    android.libraryVariants.all { variant ->
        if (variant.name == "huaweiRelease") {
            source = variant.javaCompileProvider.get().source
            classpath += files(variant.javaCompileProvider.get().classpath)
        }
    }
}
task androidHuaweiJavadocsJar(type: Jar) {
    description "Package Javadoc for Huawei."
    classifier = 'javadoc'
    from androidHuaweiJavadoc
}
task androidHuaweiSourcesJar(type: Jar) {
    description "Package sources for Huawei."
    classifier = 'sources'
    from files(android.sourceSets.main.java.srcDirs) + files(android.sourceSets.huawei.java.srcDirs)
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    pkg {
        repo = 'android'
        name = System.getProperty('bintray.pkg.name') == null ? 'swrve-sdk' : System.getProperty('bintray.pkg.name')
        userOrg = 'swrve-inc'
        licenses = ['Apache-2.0']
        desc = 'Swrve SDK'
        websiteUrl = "https://www.swrve.com"
        publish = true
        version {
            name = project.SWRVE_SDK_VERSION
        }
    }
    String bintrayPublication = System.getProperty('bintray.publication') == null ? 'core' : System.getProperty('bintray.publication')
    publications = [bintrayPublication]
}

publishing {
    repositories {
        maven {
            url "$buildDir/../publish" // SwrveSDK/publish
        }
    }

    publications {
        core(MavenPublication) {

            artifactId project.SWRVE_CORE_ARTIFACT_ID
            groupId project.SWRVE_GROUP_ID
            version project.SWRVE_SDK_VERSION
            artifacts = [androidCoreJavadocsJar, "$buildDir/outputs/aar/${project.name}-core-release.aar", androidCoreSourcesJar]

            // The publication doesn't know about our dependencies due to components.java not being present in aar pieces
            // so we have to manually add them to the pom
            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')

                //Iterate over the compile dependencies (we don't want the test ones), adding a <dependency> node for each

                configurations.getByName("coreReleaseCompileClasspath").allDependencies.each {
                    if (it.group == null || it.name == null || it.version == null) {
                        logger.log(LogLevel.ERROR, "Invalid dependency")
                    } else {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        if (it.name == 'SwrveSDKCommon') {
                            dependencyNode.appendNode('groupId', project.SWRVE_GROUP_ID)
                            dependencyNode.appendNode('artifactId', project.SWRVE_COMMON_ARTIFACT_ID)
                            dependencyNode.appendNode('version', project.SWRVE_SDK_VERSION)
                            dependencyNode.appendNode('scope', 'compile')
                        } else if (it.name == 'SwrveConversationSDK') {
                            dependencyNode.appendNode('groupId', project.SWRVE_GROUP_ID)
                            dependencyNode.appendNode('artifactId', project.SWRVE_CONVERSATIONS_ARTIFACT_ID)
                            dependencyNode.appendNode('version', project.SWRVE_SDK_VERSION)
                            dependencyNode.appendNode('scope', 'compile')
                        } else {
                            dependencyNode.appendNode('groupId', it.group)
                            dependencyNode.appendNode('artifactId', it.name)
                            dependencyNode.appendNode('version', it.version)
                            dependencyNode.appendNode('scope', 'compile')
                        }
                    }
                }
            }
        }
        amazon(MavenPublication) {
            artifactId project.SWRVE_AMAZON_ARTIFACT_ID
            groupId project.SWRVE_GROUP_ID
            version project.SWRVE_SDK_VERSION
            artifacts = [androidAmazonJavadocsJar, "$buildDir/outputs/aar/${project.name}-amazon-release.aar", androidAmazonSourcesJar]

            // The publication doesn't know about our dependencies due to components.java not being present in aar pieces
            // so we have to manually add them to the pom
            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')

                //Iterate over the compile dependencies (we don't want the test ones), adding a <dependency> node for each
                configurations.getByName("amazonReleaseCompileClasspath").allDependencies.each {
                    if (it.group == null || it.name == null || it.version == null) {
                        logger.log(LogLevel.ERROR, "Invalid dependency")
                    } else {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        if (it.name == 'SwrveSDKCommon') {
                            dependencyNode.appendNode('groupId', project.SWRVE_GROUP_ID)
                            dependencyNode.appendNode('artifactId', project.SWRVE_COMMON_ARTIFACT_ID)
                            dependencyNode.appendNode('version', project.SWRVE_SDK_VERSION)
                            dependencyNode.appendNode('scope', 'compile')
                        } else if (it.name == 'SwrveConversationSDK') {
                            dependencyNode.appendNode('groupId', project.SWRVE_GROUP_ID)
                            dependencyNode.appendNode('artifactId', project.SWRVE_CONVERSATIONS_ARTIFACT_ID)
                            dependencyNode.appendNode('version', project.SWRVE_SDK_VERSION)
                            dependencyNode.appendNode('scope', 'compile')
                        } else {
                            dependencyNode.appendNode('groupId', it.group)
                            dependencyNode.appendNode('artifactId', it.name)
                            dependencyNode.appendNode('version', it.version)
                            dependencyNode.appendNode('scope', 'compile')
                        }
                    }
                }
            }
        }
        firebase(MavenPublication) {
            artifactId project.SWRVE_FIREBASE_ARTIFACT_ID
            groupId project.SWRVE_GROUP_ID
            version project.SWRVE_SDK_VERSION
            artifacts = [androidFirebaseJavadocsJar, "$buildDir/outputs/aar/${project.name}-firebase-release.aar", androidFirebaseSourcesJar]

            // The publication doesn't know about our dependencies due to components.java not being present in aar pieces
            // so we have to manually add them to the pom
            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')

                //Iterate over the compile dependencies (we don't want the test ones), adding a <dependency> node for each
                configurations.getByName("firebaseReleaseCompileClasspath").allDependencies.each {
                    if (it.group == null || it.name == null || it.version == null) {
                        logger.log(LogLevel.ERROR, "Invalid dependency")
                    } else {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        if (it.name == 'SwrveSDKCommon') {
                            dependencyNode.appendNode('groupId', project.SWRVE_GROUP_ID)
                            dependencyNode.appendNode('artifactId', project.SWRVE_COMMON_ARTIFACT_ID)
                            dependencyNode.appendNode('version', project.SWRVE_SDK_VERSION)
                            dependencyNode.appendNode('scope', 'compile')
                        } else if (it.name == 'SwrveConversationSDK') {
                            dependencyNode.appendNode('groupId', project.SWRVE_GROUP_ID)
                            dependencyNode.appendNode('artifactId', project.SWRVE_CONVERSATIONS_ARTIFACT_ID)
                            dependencyNode.appendNode('version', project.SWRVE_SDK_VERSION)
                            dependencyNode.appendNode('scope', 'compile')
                        } else {
                            dependencyNode.appendNode('groupId', it.group)
                            dependencyNode.appendNode('artifactId', it.name)
                            dependencyNode.appendNode('version', it.version)
                            dependencyNode.appendNode('scope', 'compile')
                        }
                    }
                }
            }
        }
        huawei(MavenPublication) {
            artifactId project.SWRVE_HUAWEI_ARTIFACT_ID
            groupId project.SWRVE_GROUP_ID
            version project.SWRVE_SDK_VERSION
            artifacts = [androidHuaweiJavadocsJar, "$buildDir/outputs/aar/${project.name}-huawei-release.aar", androidHuaweiSourcesJar]

            // The publication doesn't know about our dependencies due to components.java not being present in aar pieces
            // so we have to manually add them to the pom
            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')

                //Iterate over the compile dependencies (we don't want the test ones), adding a <dependency> node for each
                configurations.getByName("huaweiReleaseCompileClasspath").allDependencies.each {
                    if (it.group == null || it.name == null || it.version == null) {
                        logger.log(LogLevel.ERROR, "Invalid dependency")
                    } else {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        if (it.name == 'SwrveSDKCommon') {
                            dependencyNode.appendNode('groupId', project.SWRVE_GROUP_ID)
                            dependencyNode.appendNode('artifactId', project.SWRVE_COMMON_ARTIFACT_ID)
                            dependencyNode.appendNode('version', project.SWRVE_SDK_VERSION)
                            dependencyNode.appendNode('scope', 'compile')
                        } else if (it.name == 'SwrveConversationSDK') {
                            dependencyNode.appendNode('groupId', project.SWRVE_GROUP_ID)
                            dependencyNode.appendNode('artifactId', project.SWRVE_CONVERSATIONS_ARTIFACT_ID)
                            dependencyNode.appendNode('version', project.SWRVE_SDK_VERSION)
                            dependencyNode.appendNode('scope', 'compile')
                        } else {
                            dependencyNode.appendNode('groupId', it.group)
                            dependencyNode.appendNode('artifactId', it.name)
                            dependencyNode.appendNode('version', it.version)
                            dependencyNode.appendNode('scope', 'compile')
                        }
                    }
                }
            }
        }
    }
}
