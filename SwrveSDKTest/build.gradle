apply plugin: 'com.android.application'

android {
    compileSdkVersion Integer.parseInt(project.ANDROID_BUILD_SDK_VERSION)
    buildToolsVersion project.ANDROID_BUILD_TOOLS_VERSION

    defaultConfig {
        minSdkVersion Integer.parseInt(project.ANDROID_BUILD_MIN_SDK_TESTS)
        targetSdkVersion Integer.parseInt(project.ANDROID_BUILD_TARGET_SDK_VERSION)
        applicationId 'com.swrve.sdk.test'
    }

    buildTypes {
        release { }
        debug {
            // Needed to run on devices and extract coverage
            debuggable true
            testCoverageEnabled true
        }
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
        def isCIRCLECI = System.getenv('CIRCLECI')
        if(isCIRCLECI != null) {
            project.logger.lifecycle('Swrve: Running in CIRCLECI. Setting maxHeapSize 1024m from SwrveSDKTest/build.gradle')
            unitTests.all {
                maxHeapSize = "1024m"
            }
        }
    }

    flavorDimensions "swrve-sdk"

    productFlavors {
        core {
            flavorDimensions "swrve-sdk"
        }
        amazon {
            flavorDimensions "swrve-sdk"
        }
        firebase {
            flavorDimensions "swrve-sdk"
        }        
    }

    lintOptions {
        abortOnError true
        baseline file("../lint/lint-baseline-swrvesdktest.xml")
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    // SDK
    api project(path: ':SwrveSDK')
    implementation 'androidx.legacy:legacy-support-v4:' + project.ANDROIDX_LEGACY_SUPPORT_V4_VERSION
    implementation 'com.google.code.gson:gson:' + project.GSON_VERSION
    implementation 'androidx.work:work-runtime:' + project.ANDROIDX_WORK_RUNTIME_VERSION

    // Unit tests
    testImplementation 'org.mockito:mockito-core:3.6.28'

    testImplementation 'com.squareup.okhttp3:mockwebserver:4.8.1'

    // Robolectric tests
    testImplementation 'junit:junit:4.13'
    testImplementation('org.robolectric:robolectric:4.4') {
        exclude group: 'commons-logging', module: 'commons-logging'
        exclude group: 'org.apache.httpcomponents', module: 'httpclient'
    }
    testImplementation 'org.robolectric:shadows-playservices:4.2'
    testImplementation 'com.google.android.gms:play-services-auth:16.0.+' // required for tests using ShadowGoogleApiAvailability and org.robolectric:shadows-playservices
    testImplementation 'androidx.test:core:1.3.0'

    testImplementation 'org.awaitility:awaitility:3.1.6'

    // The amazon-device-messaging jar is not distributed with Swrve source code and a stubbed out
    // version is used instead.
    api fileTree(dir: "../SwrveSDK/providedLibs", include: ['amazon-device-messaging*.jar'])
}

apply plugin: 'com.google.gms.google-services'
